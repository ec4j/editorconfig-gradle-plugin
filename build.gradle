/**
 * Copyright (c) 2018 EditorConfig Gradle Plugin
 * project contributors as indicated by the @author tags.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'com.gradle.plugin-publish' version '0.9.9'
    id 'java'
    id 'java-gradle-plugin'
    id 'maven'
    id 'signing'
}

group = 'org.ec4j.gradle'
version = '0.0.4-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")

sourceCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ec4j.maven:ec4j-lint-api:0.0.7'
    // Keep in sync with org.ec4j.gradle.EditorconfigGradlePlugin.LINTERS_VERSION */

    testCompile "org.junit.jupiter:junit-jupiter:5.4.2"
    testCompile gradleTestKit()
    testCompile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'org.slf4j:slf4j-simple:1.7.25'
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'org.ec4j.editorconfig'
            implementationClass = 'org.ec4j.gradle.EditorconfigGradlePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/ec4j/editorconfig-gradle-plugin'
    vcsUrl = 'https://github.com/ec4j/editorconfig-gradle-plugin.git'
    description = 'A Gradle plugin for checking whether project files comply with format rules defined in .editorconfig files and eventually also for fixing the violations'
    tags = ['editorconfig', 'lint', 'gradle', 'plugin']

    plugins {
        editorconfigPlugin {
            id = 'org.ec4j.editorconfig'
            displayName = 'EditorConfig Gradle Plugin'
        }
    }
}

test {
    testLogging.showStandardStreams = true
    testLogging {
        exceptionFormat = 'full'
    }
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar, sourcesJar
}

license {
    header = rootProject.file('license-header-template.txt')
    ignoreFailures = false
    strictCheck true
    ext.year = '2018'
    excludes([
            "**/*.adoc",
            "**/.classpath",
            "**/.gitkeep",
            "**/.project",
            ".mvn/wrapper/",
            "LICENSE",
            "gradlew",
            "gradlew.bat",
            "license-header-template.txt",
            "src/test/projects/**/*.txt"
    ])
    mapping {
        editorconfig = 'SCRIPT_STYLE'
        java = 'SLASHSTAR_STYLE'
    }
}
task licenseFormatSrc(type: com.hierynomus.gradle.license.tasks.LicenseFormat) {
    source = licenseFileTree()
}
licenseFormat.dependsOn licenseFormatSrc

task licenseSrc(type: com.hierynomus.gradle.license.tasks.LicenseCheck) {
    source = licenseFileTree()
}
licenseMain.dependsOn licenseSrc

def licenseFileTree() {
    return fileTree(dir: rootProject.projectDir).include('src/**').include('*')
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: hasProperty('ossrhUsername') ? ossrhUsername : '', password: hasProperty('ossrhPassword') ? ossrhPassword : '')
            }

            pom.project {
                name 'EditorConfig Gradle Plugin'
                packaging 'jar'
                description 'A Gradle plugin for checking whether project files comply with format rules defined in .editorconfig files and eventually also for fixing the violations'
                url 'https://github.com/ec4j/editorconfig-gradle-plugin'

                scm {
                    connection 'scm:git:https://github.com/ec4j/editorconfig-gradle-plugin.git'
                    developerConnection 'scm:git:git@github.com:ec4j/editorconfig-gradle-plugin.git'
                    url 'https://github.com/ec4j/editorconfig-gradle-plugin'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'ppalaga'
                        name 'Peter Palaga'
                    }
                }
            }
        }
    }
}
